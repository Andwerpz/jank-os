[__GLOBAL_FIRST__] u16 ARP_HW_TYPE_ETHERNET     = $u16 0x0001;
[__GLOBAL_FIRST__] u16 ARP_PROTO_TYPE_IPV4      = $u16 0x0800;
[__GLOBAL_FIRST__] u16 ARP_OPCODE_REQUEST       = $u16 0x0001;
[__GLOBAL_FIRST__] u16 ARP_OPCODE_REPLY         = $u16 0x0002;

struct ARPHeader {
    u16     hw_type;        // Hardware Type
    u16     proto_type;     // Protocol Type
    u8      hw_len;         // Hardware Length
    u8      proto_len;      // Protocol Length
    u16     opcode;         // Operation
    u8[6]   sender_mac;     // Sender Hardware Address
    u8[4]   sender_ip;      // Sender Protocol Address
    u8[6]   target_mac;     // Target Hardware Address
    u8[4]   target_ip;      // Target Protocol Address
}

void arp_handle(ARPHeader* arp) {
    u16 opcode = be_to_cpu(arp->opcode);

    if(opcode == ARP_OPCODE_REPLY) {
        println("arp_handle() : received ARP reply");
        
        print("Sender IP: ");
        print($u64 arp->sender_ip[0]); 
        print(".");
        print($u64 arp->sender_ip[1]); 
        print(".");
        print($u64 arp->sender_ip[2]); 
        print(".");
        print($u64 arp->sender_ip[3]); 
        
        print(" is at MAC: ");
        for(i32 i=0; i<6; i++) {
            print_hex(arp->sender_mac[i]);
            if(i<5) print(":");
        }
        println("");
    } else if(opcode == ARP_OPCODE_REQUEST) {
        println("arp_handle() : received ARP request");
        if( arp->target_ip[0] == IPV4_ADDR[0] &&
            arp->target_ip[1] == IPV4_ADDR[1] &&
            arp->target_ip[2] == IPV4_ADDR[2] &&
            arp->target_ip[3] == IPV4_ADDR[3]
        ) {
            println("arp_handle() : target ip matched with host, sending reply");

            u64 size = sizeof(EthernetHeader) + sizeof(ARPHeader);
            void* buffer = malloc(size);
            memset(buffer, 0, size);

            EthernetHeader* eth_reply = $EthernetHeader* buffer;
            ARPHeader* arp_reply = $ARPHeader* ($u64 buffer + sizeof(EthernetHeader));

            // set dst mac to sender mac
            for(i32 i = 0; i < 6; i++) eth_reply->dst[i] = arp->sender_mac[i];

            // set src mac
            for(i32 i=0; i < 6; i++) eth_reply->src[i] = MAC[i];

            // set ethertype
            eth_reply->type = cpu_to_be(ETHERNET_TYPE_ARP);

            // arp header
            arp_reply->hw_type = cpu_to_be(ARP_HW_TYPE_ETHERNET);
            arp_reply->proto_type = cpu_to_be(ARP_PROTO_TYPE_IPV4);
            arp_reply->hw_len = $u8 6;
            arp_reply->proto_len = $u8 4;
            arp_reply->opcode = cpu_to_be(ARP_OPCODE_REPLY);

            // set self as sender
            for(i32 i = 0; i < 6; i++) arp_reply->sender_mac[i] = MAC[i];
            for(i32 i = 0; i < 4; i++) arp_reply->sender_ip[i] = IPV4_ADDR[i];

            // set target, to src mac
            for(i32 i = 0; i < 6; i++) arp_reply->target_mac[i] = arp->sender_mac[i];
            for(i32 i = 0; i < 4; i++) arp_reply->target_ip[i] = arp->sender_ip[i];

            e1000_send_packet(buffer, $u16 size);
            
            free(buffer, size);
        } else {
            println("arp_handle() : target ip does not match with host, ignoring");
        }
    } else {
        println("arp_handle() : unknown ARP opcode");
    }
}

void send_arp_request(u8* target_ip) {
    println("send_arp_request() : sending arp packet");

    u64 size = sizeof(EthernetHeader) + sizeof(ARPHeader);
    void* buffer = malloc(size);
    memset(buffer, 0, size);

    EthernetHeader* eth = $EthernetHeader* buffer;
    ARPHeader* arp = $ARPHeader* ($u64 buffer + sizeof(EthernetHeader));

    // set dst mac to FF:FF:FF:FF:FF:FF (broadcast)
    for(i32 i = 0; i < 6; i++) eth->dst[i] = $u8 0xFF;

    // set src mac
    for(i32 i=0; i < 6; i++) eth->src[i] = MAC[i];

    // set ethertype
    eth->type = cpu_to_be(ETHERNET_TYPE_ARP);

    // arp header
    arp->hw_type = cpu_to_be(ARP_HW_TYPE_ETHERNET);
    arp->proto_type = cpu_to_be(ARP_PROTO_TYPE_IPV4);
    arp->hw_len = $u8 6;
    arp->proto_len = $u8 4;
    arp->opcode = cpu_to_be(ARP_OPCODE_REQUEST);

    // set self as sender
    for(i32 i = 0; i < 6; i++) arp->sender_mac[i] = MAC[i];
    for(i32 i = 0; i < 4; i++) arp->sender_ip[i] = IPV4_ADDR[i];

    // set target, mac is unknown (00:00:00:00:00:00)
    for(i32 i = 0; i < 6; i++) arp->target_mac[i] = $u8 0;
    for(i32 i = 0; i < 4; i++) arp->target_ip[i] = target_ip[i];

    // send
    e1000_send_packet(buffer, $u16 size);
    
    free(buffer, size);
}
