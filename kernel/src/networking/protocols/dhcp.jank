// https://en.wikipedia.org/wiki/Dynamic_Host_Configuration_Protocol#Operationv

[__GLOBAL_FIRST__] u16 DHCP_SERVER_LISTEN_PORT = $u16 67;
[__GLOBAL_FIRST__] u16 DHCP_CLIENT_LISTEN_PORT = $u16 68;

[__GLOBAL_FIRST__] u8  DHCP_OP_BOOTREQUEST  = $u8 1;
[__GLOBAL_FIRST__] u8  DHCP_OP_BOOTREPLY    = $u8 2;

[__GLOBAL_FIRST__] u8  DHCP_HTYPE_ETHERNET  = $u8 1;
[__GLOBAL_FIRST__] u8  DHCP_HLEN_ETHERNET   = $u8 6;

[__GLOBAL_FIRST__] u32 DHCP_XID = $u32 0x12345678; // uh this should be random or something, but just needs to match for the reply for now heh

[__GLOBAL_FIRST__] u16 DHCP_FLAGS_BROADCAST = $u16 0x8000;

[__GLOBAL_FIRST__] u32 DHCP_MAGIC_COOKIE = $u32 0x63825363;

// options are packed using Type Length Value (TLV)
// https://en.wikipedia.org/wiki/Type%E2%80%93length%E2%80%93value
[__GLOBAL_FIRST__] u8  DHCP_OPT_PAD             = $u8 0;
[__GLOBAL_FIRST__] u8  DHCP_OPT_SUBNET_MASK     = $u8 1;
[__GLOBAL_FIRST__] u8  DHCP_OPT_ROUTER          = $u8 3;
[__GLOBAL_FIRST__] u8  DHCP_OPT_DNS             = $u8 6;
[__GLOBAL_FIRST__] u8  DHCP_OPT_REQUESTED_IP    = $u8 50;
[__GLOBAL_FIRST__] u8  DHCP_OPT_MESSAGE_TYPE    = $u8 53;
[__GLOBAL_FIRST__] u8  DHCP_OPT_SERVER_ID       = $u8 54;
[__GLOBAL_FIRST__] u8  DHCP_OPT_PARAM_REQ_LIST  = $u8 55;
[__GLOBAL_FIRST__] u8  DHCP_OPT_END             = $u8 255;

[__GLOBAL_FIRST__] u8  DHCP_MSG_TYPE_DISCOVER   = $u8 1;
[__GLOBAL_FIRST__] u8  DHCP_MSG_TYPE_OFFER      = $u8 2;
[__GLOBAL_FIRST__] u8  DHCP_MSG_TYPE_REQUEST    = $u8 3;
[__GLOBAL_FIRST__] u8  DHCP_MSG_TYPE_ACK        = $u8 5;

struct DHCPHeader {
    u8      op;                 // packet type
    u8      htype;              // hardware address type
    u8      hlen;               // length of hardware addr
    u8      hops;               // hops
    u32     xid;                // random transaction number
    u16     secs;               // seconds usted in timing
    u16     flags;              // flags
    u32     client_ip_addr;     // ip addr of this machine (if we alr have)
    u32     your_ip_addr;       // ip addr of this machine (offered by dhcp server)
    u32     server_ip_addr;     // dhcp server ip addr
    u32     gateway_ip_addr;    // dhcp relay ip addr
    u8[16]  client_hw_addr;     // hardware addrs
    u8[192] bootp_legacy;       // make this all 0 hehe
    u32     magic_cookie;       // need this to differentiate bootp from dhcp
}

struct DHCPDiscoverOptions {
    // option 53
    u8 opt_msg_type_code;
    u8 opt_msg_type_len;
    u8 opt_msg_type_val;

    // option 55
    u8 opt_req_list_code;
    u8 opt_req_list_len;
    u8 req_item_subnet;
    u8 req_item_router;
    u8 req_item_dns;

    // end
    u8 opt_end;
}

struct DHCPRequestOptions {
    // option 53
    u8 opt_msg_type_code;
    u8 opt_msg_type_len;
    u8 opt_msg_type_val;

    // option 50
    u8 opt_req_ip_code;
    u8 opt_req_ip_len;
    u32 opt_req_ip_val;

    // option 54
    u8 opt_server_id_code;
    u8 opt_server_id_len;
    u32 opt_server_id_val;

    // option 55
    u8 opt_req_list_code;
    u8 opt_req_list_len;
    u8 req_item_subnet;
    u8 req_item_router;
    u8 req_item_dns;

    // end
    u8 opt_end;
}

void dhcp_handle(DHCPHeader* dhcp, u16 len) {
    if(NETWORKING_APPLICATION_LAYER_DEBUG) println("dhcp_handle() : Handling incoming DHCP message");

    if(len < $u16 sizeof(UDPHeader)) {
        if(NETWORKING_APPLICATION_LAYER_DEBUG) println("dhcp_handle() : DHCP header length too short");
        return;
    }

    if(be_to_cpu(dhcp->magic_cookie) != DHCP_MAGIC_COOKIE) {
        if(NETWORKING_APPLICATION_LAYER_DEBUG) println("dhcp_handle() : Invalid magic cookie for DHCP");
        return;
    }

    if(dhcp->op != DHCP_OP_BOOTREPLY) {
        if(NETWORKING_APPLICATION_LAYER_DEBUG) println("dhcp_handle() : Incoming DHCP message is a request, ignoring");
        return;
    }

    if(dhcp->htype != DHCP_HTYPE_ETHERNET || dhcp->hlen != DHCP_HLEN_ETHERNET) {
        if(NETWORKING_APPLICATION_LAYER_DEBUG) println("dhcp_handle() : Incoming DHCP unsupported hardware type or length, ignoring");
        return;
    }

    if(be_to_cpu(dhcp->xid) != DHCP_XID) {
        if(NETWORKING_APPLICATION_LAYER_DEBUG) println("dhcp_handle() : Invalid XID, ignoring");
        return;
    }

    u8* options = $u8* ($u64 dhcp + sizeof(DHCPHeader));
    u8 type = $u8 0;
    for(u64 i = 0x0; i < $u64 len - sizeof(DHCPHeader);) {
        if(options[i] == DHCP_OPT_END) break;
        if(options[i] == DHCP_OPT_PAD) {
            i++;
            continue;
        }

        u8 opt_code = options[i];
        u8 opt_len = options[i + $u64 1];

        if(opt_code == DHCP_OPT_MESSAGE_TYPE) {
            type = options[i + $u64 2];
            break;
        }

        i += ($u64 2 + $u64 opt_len); // skip the type and len field, then skip by length
    }

    if(type == DHCP_MSG_TYPE_OFFER) {
        if(NETWORKING_APPLICATION_LAYER_DEBUG) println("dhcp_handle() : Received DHCP offer");
        dhcp_handle_offer(dhcp, len);
    } else if(type == DHCP_MSG_TYPE_ACK) {
        if(NETWORKING_APPLICATION_LAYER_DEBUG) println("dhcp_handle() : Received DHCP ack");
        dhcp_handle_ack(dhcp, len);
    } else {
        if(NETWORKING_APPLICATION_LAYER_DEBUG) println("dhcp_handle() : Unknown message type");
    }
}

void dhcp_handle_ack(DHCPHeader* dhcp, u16 len) {
    if(NETWORKING_APPLICATION_LAYER_DEBUG) println("dhcp_handle_ack() : Handling incoming DHCP ack");

    CURRENT_INTERFACE->ip_address = be_to_cpu(dhcp->your_ip_addr);
    
    u8* options = $u8* ($u64 dhcp + sizeof(DHCPHeader));

    for(u64 i = 0x0; i < $u64 len - sizeof(DHCPHeader);) {
        if(options[i] == DHCP_OPT_END) break;
        if(options[i] == DHCP_OPT_PAD) {
            i++;
            continue;
        }

        u8 opt_code = options[i];
        u8 opt_len = options[i + $u64 1];

        if(opt_code == DHCP_OPT_SERVER_ID) {
            u32* dhcp_server = $u32* @options[i + $u64 2];
            CURRENT_INTERFACE->dhcp_server = be_to_cpu(*dhcp_server);
        } else if(opt_code == DHCP_OPT_SUBNET_MASK) {
            u32* subnet_mask = $u32* @options[i + $u64 2];
            CURRENT_INTERFACE->subnet_mask = be_to_cpu(*subnet_mask);
        } else if(opt_code == DHCP_OPT_ROUTER) {
            u32* gateway_ip = $u32* @options[i + $u64 2];
            CURRENT_INTERFACE->gateway_ip = be_to_cpu(*gateway_ip);
        } else if(opt_code == DHCP_OPT_DNS) {
            u32* dns_server = $u32* @options[i + $u64 2];
            CURRENT_INTERFACE->dns_server = be_to_cpu(*dns_server);
        }

        i += ($u64 2 + $u64 opt_len); // skip the type and len field, then skip by length
    }
    
    dump_network_interface(CURRENT_INTERFACE);
    CURRENT_INTERFACE->dhcp_configured = 1;
}

void dhcp_handle_offer(DHCPHeader* dhcp, u16 len) {
    if(NETWORKING_APPLICATION_LAYER_DEBUG) println("dhcp_handle_offer() : Handling incoming DHCP offer");

    u32 offered_ip = be_to_cpu(dhcp->your_ip_addr);
    u32 server_ip = $u32 0;
    
    u8* options = $u8* ($u64 dhcp + sizeof(DHCPHeader));

    for(u64 i = 0x0; i < $u64 len - sizeof(DHCPHeader);) {
        if(options[i] == DHCP_OPT_END) break;
        if(options[i] == DHCP_OPT_PAD) {
            i++;
            continue;
        }

        u8 opt_code = options[i];
        u8 opt_len = options[i + $u64 1];

        if(opt_code == DHCP_OPT_SERVER_ID) {
            u32* dhcp_server = $u32* @options[i + $u64 2];
            server_ip = be_to_cpu(*dhcp_server);
        }

        i += ($u64 2 + $u64 opt_len); // skip the type and len field, then skip by length
    }
    
    if(NETWORKING_APPLICATION_LAYER_DEBUG) println("dhcp_handle_offer() : Offer parsed, sending request");
    dhcp_send_request(offered_ip, server_ip);
}

void dhcp_send_request(u32 offered_ip, u32 server_ip) {
    if(NETWORKING_APPLICATION_LAYER_DEBUG) {
        print("dhcp_send_request() : Sending DHCP request to [");
        print_ipv4_addr(server_ip);
        println("]");
    }

    u64 total_len = sizeof(DHCPHeader) + sizeof(DHCPRequestOptions);
    u8* buffer = $u8* malloc(total_len);
    memset($void* buffer, 0, total_len);

    DHCPHeader* dhcp = $DHCPHeader* buffer;
    DHCPRequestOptions* options = $DHCPRequestOptions* ($u64 buffer + sizeof(DHCPHeader));

    dhcp->op = DHCP_OP_BOOTREQUEST;
    dhcp->htype = DHCP_HTYPE_ETHERNET;
    dhcp->hlen = DHCP_HLEN_ETHERNET;
    dhcp->hops = $u8 0;
    dhcp->xid = cpu_to_be(DHCP_XID);
    dhcp->secs = $u16 0;
    dhcp->flags = cpu_to_be(DHCP_FLAGS_BROADCAST);

    // we dont technically own the ip yet so we cant say we do
    dhcp->client_ip_addr = $u32 0;
    dhcp->your_ip_addr = $u32 0;
    dhcp->server_ip_addr = $u32 0;
    dhcp->gateway_ip_addr = $u32 0;

    memcpy($void* @dhcp->client_hw_addr[0], $void* @CURRENT_INTERFACE->mac_address, $u64 6);

    dhcp->magic_cookie = cpu_to_be(DHCP_MAGIC_COOKIE);

    options->opt_msg_type_code = DHCP_OPT_MESSAGE_TYPE;
    options->opt_msg_type_len = $u8 1;
    options->opt_msg_type_val = DHCP_MSG_TYPE_REQUEST;

    options->opt_req_ip_code = DHCP_OPT_REQUESTED_IP;
    options->opt_req_ip_len = $u8 4;
    options->opt_req_ip_val = cpu_to_be(offered_ip);

    options->opt_server_id_code = DHCP_OPT_SERVER_ID;
    options->opt_server_id_len = $u8 4;
    options->opt_server_id_val = cpu_to_be(server_ip);

    options->opt_req_list_code = DHCP_OPT_PARAM_REQ_LIST;
    options->opt_req_list_len = $u8 3;
    options->req_item_subnet = DHCP_OPT_SUBNET_MASK;
    options->req_item_router = DHCP_OPT_ROUTER;
    options->req_item_dns = DHCP_OPT_DNS;

    options->opt_end = DHCP_OPT_END;

    udp_send_packet(IPv4_BROADCAST_ADDR, DHCP_CLIENT_LISTEN_PORT, DHCP_SERVER_LISTEN_PORT, buffer, $u16 total_len);

    free($void* buffer, total_len);
}

void dhcp_send_discover() {
    if(NETWORKING_APPLICATION_LAYER_DEBUG) println("dhcp_send_discover() : Sending DHCP discover");

    u64 total_len = sizeof(DHCPHeader) + sizeof(DHCPDiscoverOptions);
    u8* buffer = $u8* malloc(total_len);
    memset($void* buffer, 0, total_len);

    DHCPHeader* dhcp = $DHCPHeader* buffer;
    DHCPDiscoverOptions* options = $DHCPDiscoverOptions* ($u64 buffer + sizeof(DHCPHeader));

    dhcp->op = DHCP_OP_BOOTREQUEST;
    dhcp->htype = DHCP_HTYPE_ETHERNET;
    dhcp->hlen = DHCP_HLEN_ETHERNET;
    dhcp->hops = $u8 0;
    dhcp->xid = cpu_to_be(DHCP_XID);
    dhcp->secs = $u16 0;
    dhcp->flags = cpu_to_be(DHCP_FLAGS_BROADCAST);
    dhcp->client_ip_addr = $u32 0;
    dhcp->your_ip_addr = $u32 0;
    dhcp->server_ip_addr = $u32 0;
    dhcp->gateway_ip_addr = $u32 0;

    memcpy($void* @dhcp->client_hw_addr[0], $void* @CURRENT_INTERFACE->mac_address, $u64 6);

    dhcp->magic_cookie = cpu_to_be(DHCP_MAGIC_COOKIE);

    options->opt_msg_type_code = DHCP_OPT_MESSAGE_TYPE;
    options->opt_msg_type_len = $u8 1;
    options->opt_msg_type_val = DHCP_MSG_TYPE_DISCOVER;

    options->opt_req_list_code = DHCP_OPT_PARAM_REQ_LIST;
    options->opt_req_list_len = $u8 3;
    options->req_item_subnet = DHCP_OPT_SUBNET_MASK;
    options->req_item_router = DHCP_OPT_ROUTER;
    options->req_item_dns = DHCP_OPT_DNS;

    options->opt_end = DHCP_OPT_END;

    udp_send_packet(IPv4_BROADCAST_ADDR, DHCP_CLIENT_LISTEN_PORT, DHCP_SERVER_LISTEN_PORT, buffer, $u16 total_len);

    free($void* buffer, total_len);
}

void init_dhcp() {
    if(net_interfaces.size() == $u64 0) {
        return;
    }

    if(CURRENT_INTERFACE->dhcp_configured) {
        return; 
    }

    if(UDP_PORT_TABLE[DHCP_CLIENT_LISTEN_PORT] == nullptr) {
        // make dummy dhcp socket
        socket* socket = $socket* malloc(sizeof(socket));
        new (socket) socket();
        UDP_PORT_TABLE[DHCP_CLIENT_LISTEN_PORT] = socket;
    }

    dhcp_send_discover();
}